buildscript {
    repositories {
        maven { url = 'https://maven.maxhenkel.de/repository/public' }
        mavenLocal()
    }
    dependencies {
        classpath group: 'de.maxhenkel.forge-update', name: 'forge-update', version: '1.0.4'
    }
}

plugins {
    id "fabric-loom" version "0.10-SNAPSHOT"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

apply plugin: 'forge-update'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
}

runClient.doFirst {
    args = ['--username', "henkelmax${new Random().nextInt(1000)}"]
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    Set<String> apiModules = [
            "fabric-command-api-v1",
            "fabric-lifecycle-events-v1",
            "fabric-networking-api-v1",
            "fabric-key-binding-api-v1",
            "fabric-rendering-v1",
            "fabric-resource-loader-v0"
    ]

    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, project.fabric_version)))
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'de.maxhenkel.configbuilder:configbuilder:1.0.0'
    shadow 'de.maxhenkel.configbuilder:configbuilder:1.0.0'

    implementation 'de.maxhenkel.opus4j:opus4j:1.0.2'
    shadow 'de.maxhenkel.opus4j:opus4j:1.0.2'

    implementation 'de.maxhenkel.rnnoise4j:rnnoise4j:1.0.1'
    shadow 'de.maxhenkel.rnnoise4j:rnnoise4j:1.0.1'
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_dependency": project.minecraft_dependency,
                "loader_version": project.loader_version,
                "fabric_version": project.fabric_version,
                "compatibility_version": project.mod_compatibility_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

curseforge {
    apiKey = file('curseforge_api_key.txt').exists() ? file('curseforge_api_key.txt').text : ''
    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = curse_release_type
        addGameVersion curseforge_minecraft_version
        addGameVersion "Fabric"
        addGameVersion 'Java 16'
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "[FABRIC][${minecraft_version}] ${mod_name} ${mod_version}"
            relations {
                requiredDependency 'fabric-api'
            }
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

forgeUpdate {
    def messages = []
    file('changelog.md').eachLine { String line ->
        if (line.trim().startsWith('-')) {
            messages.add(line.replaceFirst('-', '').trim())
        }
    }

    serverURL = 'https://update.maxhenkel.de/'
    apiKey = file('forge_update_api_key.txt').exists() ? file('forge_update_api_key.txt').text : ''
    modID = mod_id
    gameVersion = minecraft_version
    modLoader = "fabric"
    modVersion = mod_version
    updateMessages = messages
    releaseType = curse_release_type
    tags = recommended == 'true' ? ['recommended'] : []
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier 'shadow-dev'
    relocate 'de.maxhenkel.configbuilder', "de.maxhenkel.${mod_id}.configbuilder"
    relocate 'de.maxhenkel.opus4j', "de.maxhenkel.${mod_id}.opus4j"
    relocate 'de.maxhenkel.rnnoise4j', "de.maxhenkel.${mod_id}.rnnoise4j"
    dependencies {
        exclude(dependency('net.java.dev.jna:jna'))
    }
}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile.get()
}