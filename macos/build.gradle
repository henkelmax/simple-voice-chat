buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version as int)

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.googlecode.plist:dd-plist:1.23'
    shadow 'com.googlecode.plist:dd-plist:1.23'
    shadow 'net.java.dev.jna:jna:4.4.0'
    shadow 'net.java.dev.jna:jna-platform:4.4.0'

}

task prepareWorkspace {

}

processResources{
    filesMatching("patcher.properties") {
        expand 'patcher_version': patcher_version
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'de.maxhenkel.voicechat.macos.Main'
    }
}

build.dependsOn shadowJar

shadowJar {
    classifier ''
    relocate 'com.dd.plist', 'de.maxhenkel.voicechat.com.dd.plist'
    dependencies {
        exclude(dependency('net.java.dev.jna:jna'))
        exclude(dependency('net.java.dev.jna:jna-platform'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task standaloneJar(type: ShadowJar) {
    group = 'build'
    configurations = [project.configurations.shadow]
    classifier ''
    baseName 'simple-voice-chat-macos-workaround'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'de.maxhenkel.voicechat.macos.StandaloneMain'
    }
}